5 GOTO 500
10 PRINT"PROGRAM HALTED ":PRINT"EXECUTION TIME:";(TI-T)/60;"S":END
20 S(SP)=P(PC)*256+P(PC+1):PC=PC+2:SP=SP+1:GOTO 810
40 SP=SP-1:S(SP-1)=S(SP-1)-S(SP):GOTO 810
70 PC=P(PC)*256+P(PC+1):GOTO 810
90 SP=SP-1:IF S(SP)<0 THEN PC = P(PC)*256+P(PC+1):GOTO 810
91 PC=PC+2:GOTO 810
100 S(SP)=S(SP-1):SP=SP+1:GOTO 810
110 SP=SP-1:PRINT S(SP):GOTO 810
500 PRINT"+---------------------------+"
510 PRINT"!      EDSBVM V0.0 DEV      !"
520 PRINT"!(C)2017 IR.MARC DENDOOVEN  !"
530 PRINT"+---------------------------+"
600 REM *** SETTINGS ***
610 MS=1023:REM MAXSTACK
620 MP=1023:REM MAXPROG
650 REM *** INIT ***
660 DIM S(MS),P(MP)
700 SP=0
710 OPEN 8,8,8,"A.OUT,S,R"
715 IF ST AND 64 GOTO 780
720 GET#8,O$:IF O$="" THEN O$=CHR$(0)
730 P(PC)=ASC(O$)
740 PC=PC+1
750 GOTO 715
780 CLOSE 8
799 REM *** MAIN ***
800 PC=0
805 T=TI
810 IR=P(PC):PC=PC+1
820 ON IR+1 GOTO 810,10,20,840,40,840,840,70,840,90,100,110
840 PRINT"UNKNOWN OPCODE ";IR
850 STOP
860 GOTO 810
